PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX t: <http://www.onto.net/core/>
PREFIX a: <http://www.onto.net/abox/heads-and-tails/>

SELECT ?type ?id
#store
?guard ?code ?arc ?arc_type ?place
#calc (places & arcs)
?term ?term_value
?value
#FROM <urn:x-arq:DefaultGraph>
FROM <http://localhost:3030/ontonet/data/tbox>
FROM <http://localhost:3030/ontonet/data/abox>
WHERE {
  ?cpn rdf:type t:CPN.
  {
    ?cpn t:has_node ?transition.
    ?transition rdf:type t:Transition.
    BIND (?transition as ?id)
    BIND ("transitions" as ?type)
    {
      ?transition t:has_guard ?guard_term.
      ?guard_term t:has_value ?guard.
      OPTIONAL {
        ?transition t:has_code ?code_term.
        ?code_term t:has_value ?code.
      }
    }
    UNION
    {
      ?arc rdf:type t:Arc.
      {
        ?arc t:has_sourceNode ?place;
             t:has_targetNode ?transition.
        BIND("input" as ?arc_type)
      }
      UNION
      {
        ?arc t:has_sourceNode ?transition;
             t:has_targetNode ?place.
        BIND("output" as ?arc_type)
      }
    }
  }
  UNION
  {
    {
      ?arc_or_place rdf:type t:Arc;
                    t:has_annotation ?_term.
      BIND ("arcs" as ?type)
    }
    UNION
    {
      ?arc_or_place rdf:type t:Place;
                    t:has_initialTokens ?_term.
      BIND ("places" as ?type)
    }
    BIND (?arc_or_place as ?id)
    OPTIONAL {
      ?eval rdf:type t:Evaluation;
            t:has_term ?_term;
            t:has_data ?data.
      ?data t:has_value ?value.
    }
    OPTIONAL {
      FILTER(!BOUND(?value))
      BIND(?_term as ?term)
      ?_term t:has_value ?term_value.
    }
  }
}
ORDER BY ?type ?arc