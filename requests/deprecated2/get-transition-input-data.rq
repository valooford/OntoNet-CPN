FROM <urn:x-arq:DefaultGraph>
FROM <${this.endpointUrl}/data/net>
:T1 -> ${uri}
##########################################################################

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX core: <http://www.onto.net/core/>
PREFIX js: <http://www.onto.net/js/>
PREFIX : <http://www.experimental.onto.net/js-core/net/heads-and-tails/>

SELECT ?type
       ?code ?guard_value #transition
       ?arc_i ?place_i #arcs
       ?arcan_constant_name ?term_value ?multiplicity #arcs_data
FROM <http://localhost:3030/ontonet/data/net>
WHERE {
  {
    BIND("transition" as ?type)
    #code
    OPTIONAL {
      :T1 core:has_code ?code.
    }
    #guard
    OPTIONAL {
      :T1 core:has_guard ?guard.
      ?guard core:has_value ?_guard_value.
    }
    BIND (if(!BOUND(?_guard_value), "true", ?_guard_value) as ?guard_value)
  }
  UNION
  {
    #input arcs
    ?arc_i core:has_targetNode :T1.
    {
      BIND("arcs" as ?type)
      ?arc_i core:has_sourceNode ?place_i.
    }
    UNION
    {
      BIND("arcs_data" as ?type)
      OPTIONAL {
        ?arc_i core:has_annotation ?arc_i_annotation.
        ?arc_i_annotation core:has_multisetOfTerms ?arcan_multiset.
        {
          ?arcan_multiset rdf:type core:Constant.
          BIND (?arcan_multiset as ?arcan_constant)
          ?arcan_constant core:has_name ?arcan_constant_name.
        }
        UNION
        {
          ?arcan_multiset core:includes_basisSet ?basisSet.
          ?basisSet core:has_data ?term;
                    core:has_multiplicity ?multiplicity.
          ?term core:has_value ?term_value.
        }
      }
    }
  }
}
ORDER BY ?arc_i