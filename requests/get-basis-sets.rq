PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX t: <http://www.onto.net/core/>
PREFIX a: <http://www.onto.net/abox/heads-and-tails/>

SELECT ?transition ?basis_set_a ?value_a ?multiplicity_a ?basis_set_p ?value_p ?multiplicity_p
FROM <http://localhost:3030/ontonet/data/tbox>
FROM <http://localhost:3030/ontonet/data/abox>
WHERE {
  ?cpn rdf:type t:CPN;
       t:has_node ?transition;
       t:has_marking ?marking.
  ?transition rdf:type t:Transition.
  ?arc rdf:type t:Arc;
       t:has_targetNode ?transition;
       t:has_sourceNode ?place;
       t:has_multisetOfTerms ?multiset_a.
  ?multiset_a t:has_basisSet ?basis_set_a.
  ?basis_set_a t:has_data ?data_a;
               t:has_multiplicity ?multiplicity_a.
  ?data_a t:has_value ?value_a.
  
  #last marking
  FILTER NOT EXISTS {
    ?firing rdf:type t:Firing;
            t:has_sourceMarking ?marking.
  }
  ?marking t:includes_markingOfPlace ?mop.
  ?mop t:has_place ?place;
       t:has_multisetOfTokens ?multiset_p.
  OPTIONAL {
    ?multiset_p t:has_basisSet ?basis_set_p.
    ?basis_set_p t:has_data ?data_p;
                 t:has_multiplicity ?multiplicity_p.
    ?data_p t:has_value ?value_p.
  }
}