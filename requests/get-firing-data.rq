PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX t: <http://www.onto.net/core/>
PREFIX a: <http://www.onto.net/abox/heads-and-tails/>

SELECT ?type
?id
?variable_name ?value
?anno_chunk_bs ?token_bs
FROM <http://localhost:3030/ontonet/data/tbox>
FROM <http://localhost:3030/ontonet/data/abox>
WHERE {
  ?firing rdf:type t:Firing. 
  # replace ?firing with <uri>
  ?firing t:has_multisetOfTransitionModes ?multiset.
  ?multiset t:has_basisSet ?basis_set.
  ?basis_set t:has_data ?transition_mode.
  
  {
    BIND ("multiplicity" as ?type)
    {
      SELECT DISTINCT ?transition_mode ?id ?value
      WHERE {
        ?transition_mode t:has_binding ?binding.
        {
          ?binding t:has_annotationChunk ?anno_chunk_bs.
          ?anno_chunk_bs t:has_multiplicity ?anno_chunk_multiplicity.
          BIND (?anno_chunk_bs as ?id)
          BIND (?anno_chunk_multiplicity as ?value)
        }
        UNION
        {
          ?binding t:has_token ?token_bs.
          ?token_bs t:has_multiplicity ?token_multiplicity.
          BIND (?token_bs as ?id)
          BIND (?token_multiplicity as ?value)
        }
      }
    }
  }
  UNION
  {
    BIND ("relation" as ?type)
    {
      SELECT DISTINCT ?transition_mode ?anno_chunk_bs ?token_bs
      WHERE {
        ?transition_mode t:has_binding ?binding.
        ?binding t:has_annotationChunk ?anno_chunk_bs;
                 t:has_token ?token_bs.
      }
    }
  }
  UNION
  {
    BIND ("transition" as ?type)
    {
      SELECT ?transition_mode ?id
      WHERE {
        ?transition_mode t:has_transition ?transition.
        BIND (?transition as ?id)
      }
    }
  }
  UNION
  {
    BIND ("binding" as ?type)
    {
      SELECT DISTINCT ?transition_mode ?variable_name ?value
      WHERE {
        ?transition_mode t:has_binding ?binding.
        ?binding t:has_variable ?variable;
		         t:has_data ?data.
        ?variable t:has_value ?variable_name.
        ?data t:has_value ?value.
      }
    }
  }
}
ORDER BY ?type
